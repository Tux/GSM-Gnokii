#!/pro/bin/perl

use strict;
use warnings;
use autodie;
use Data::Peek;

sub usage
{
    my $err = shift and select STDERR;
    print "usage: $0 [--depth=0] [ME|SM] [path]\n";
    exit $err;
    } # usage

use Getopt::Long qw(:config bundling);
my $depth = 0;
my $gui   = 0;
my $opt_v = 1;
GetOptions (
    "help|?"		=> sub { usage (0); },

    "d|depth=i"		=> \$depth,
    "x|x11|gui"		=> \$gui,

    "v|verbose:2"	=> \$opt_v,
    ) or usage (1);

my @mt;
my @path;
for (@ARGV) {
    if (m/^(me|sm)$/i) {
	push @mt, uc $_;
	}
    else {
	s{[/\\]*$}{};
	s{^[/\\]*}{/};
	s{/}{\\}g;
	push @path, $_;
	}
    }

@mt or @mt = qw( ME SM );

use GSM::Gnokii;
use JSON;

my $gsm = GSM::Gnokii->new ({ verbose => $opt_v })->connect;
END { $gsm->disconnect; }

$opt_v >= 9 and DDumper { mt => \@mt, path => \@path };

unless ($gui) {
    binmode STDOUT, ":encoding(utf-8)";

    my %dt;
    foreach my $mt (@mt) {
	$dt{$mt} = @path
	    ? [ map { [ $gsm->GetDir ($mt, $_, $depth) ] } @path ]
	    : $gsm->GetDirTree ($mt, $depth);
	}

    print to_json (\%dt, {
	utf8	=> 1,
	pretty	=> 1,
	});
    exit 0;
    }

use Tk;
use Tk::Gnokii::GSMTree;

my $mw = MainWindow->new;

my $dt = $mw->GSMTree (
    -gsm     => $gsm,
    -memtype => "ME",
    -width   => 40,
    -height  => 80,
    -browsecmd => \&x_info,
    )->pack (-side => "left", -anchor => "nw");
my $fi = $mw->Frame ()->pack (-side => "left", -anchor => "nw");
my %fi;
for (qw( name date size id type folder_id )) {
    my $f = $fi->Frame ()->pack (qw( -side top -expand 1 -fill x ));
    $f->Label (-text => $_, -width => 10, -foreground => "Green4", -anchor => "w")->pack (qw( -side left -anchor w -fill both -expand 1 ));
    $f->Label (-textvariable => \$fi{"f_$_"}, -width => 40, -anchor => "w")->pack (qw( -side left -anchor w -fill both -expand 1 ));
    }

$mw->update;
$dt->chdir ("/");
$mw->update;
#$dt->chdir ("/predefgallery");
#$mw->update;
#$dt->chdir ("/predefgallery/predefgraphics");
#$mw->update;
if ($opt_v > 1) {
    open my $log, ">", "cache.out";
    print $log scalar $dt->show_cache ();
    close $log;
    }

MainLoop;

sub x_info
{
    $opt_v > 3 and print STDERR "XI (@{[join', '=>@_]})\n";
    @_ == 1 or return;
    my $dir = shift;
    my $info = $dt->fileinfo ($dir);
    $_ = "" for values %fi;
    $fi{$_} = $info->{$_} for keys %$info;
    $opt_v > 2 and print STDERR DDumper $info;
    } # x_info
